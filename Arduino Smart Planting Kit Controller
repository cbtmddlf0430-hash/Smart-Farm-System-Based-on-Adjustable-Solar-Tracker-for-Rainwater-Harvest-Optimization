#include <LiquidCrystal_I2C.h>/*LiquidCrystal_I2C*/

#define A_SOIL_HUMI A0
#define O_RGB_R 10
#define O_RGB_G 9
#define O_RGB_B 11

#define O_PWM_1 6
#define O_PWM_2 7

#define LCD_I2C_ADDR 0x27

#define On_Time 2000

#define PUMP_OFF 0
#define PUMP_START 170
#define PUMP_MAX 230

LiquidCrystal_I2C lcd(LCD_I2C_ADDR, 16, 2);

int soilHumidity;

/*디지털핀 초기화하기*/
void initPin() {

  pinMode(O_RGB_R, OUTPUT);
  pinMode(O_RGB_G, OUTPUT);
  pinMode(O_RGB_B, OUTPUT);

  digitalWrite(O_RGB_R, LOW);
  digitalWrite(O_RGB_G, LOW);
  digitalWrite(O_RGB_B, LOW);

  pinMode(O_PWM_1, OUTPUT);
  pinMode(O_PWM_2, OUTPUT);

  analogWrite(O_PWM_1, 0);
  digitalWrite(O_PWM_2, 0);
}

/*LCD INTRO출력하기*/
void introLcd() {
  lcd.print("Planting Kit");
  lcd.setCursor(0, 1);
  lcd.print("Rev4.0");
}

/*LCD 초기화하기*/
void initLcd() {
  lcd.init();
  lcd.backlight();
  lcd.setCursor(0, 0);
  introLcd();
}

/*LCD 습도 프린트하기*/
void printLcd() {
  //lcd.init();
  lcd.clear();
  lcd.backlight();
  lcd.setCursor(0, 0);
  lcd.print("Moisture : ");
  lcd.print(soilHumidity);
  lcd.print("%");
  lcd.setCursor(0, 1);
  if (soilHumidity < 30) lcd.print("Need Water");
  else if (soilHumidity < 50) lcd.print("Soil is Dry");
  else if (soilHumidity < 90) lcd.print("Soil is Wet");
  else lcd.print("Enough Water");

}

/*토양습도 계산하기*/
void calcSoilHumidity() {
  soilHumidity = map(analogRead(A_SOIL_HUMI), 1000, 400, 0, 100);
  if (soilHumidity > 100) soilHumidity = 100;
  else if (soilHumidity < 0) soilHumidity = 0;
}

void writeRGB(bool R, bool G, bool B) {
  digitalWrite(O_RGB_R, R);
  digitalWrite(O_RGB_G, G);
  digitalWrite(O_RGB_B, B);
}

void setup() {
  initPin();
  initLcd();
  delay(2000);
  //RGB LED를 보라색(빨강+파랑)으로 출력합니다.
  writeRGB(HIGH, LOW, HIGH);
}

void loop() {

  calcSoilHumidity();

  printLcd();

  /* 습도가 낮아지면, 펌프가 동작되고
     습도가 높아지면, 펌프가 꺼집니다. */
  if (soilHumidity < 30)
  {
    delay(2000);
    lcd.clear();
    lcd.noBacklight();
    delay(250);

    for (int i = PUMP_START; i < PUMP_MAX; i++)
    {
      analogWrite(O_PWM_1, i);
      delay(5);
    }
    delay(On_Time);
    analogWrite(O_PWM_1, PUMP_OFF);
    delay(250);
  }
  else {
    analogWrite(O_PWM_1, PUMP_OFF);
  }
}
